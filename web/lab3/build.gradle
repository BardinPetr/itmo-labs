plugins {
  id 'java'
  id 'war'
  id "mx.com.inftel.wildfly" version "1.0.2"
  id "io.freefair.lombok" version "8.3"
  id "com.github.node-gradle.node" version "7.0.0"
}

group 'ru.bardinpetr.itmo'
version '0.1'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.9.1'
}

sourceCompatibility = "18"
targetCompatibility = "18"

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

dependencies {
//  compileOnly 'javax.security.enterprise:javax.security.enterprise-api:1.0'

  // WEB
  compileOnly 'jakarta.servlet:jakarta.servlet-api:5.0.0'

  // JSF
  compileOnly 'jakarta.faces:jakarta.faces-api:3.0.0'
  implementation 'org.glassfish:jakarta.faces:3.0.3'

  compileOnly 'jakarta.el:jakarta.el-api:4.0.0'
  implementation 'org.glassfish:jakarta.el:4.0.2'

  implementation 'org.primefaces:primefaces:13.0.0:jakarta'

  // Beans
  implementation 'jakarta.enterprise:jakarta.enterprise.cdi-api:3.0.0'
  implementation 'org.jboss.weld.se:weld-se-core:4.0.3.Final'
  implementation 'jakarta.annotation:jakarta.annotation-api:2.0.0'

  // validation
  compileOnly 'jakarta.validation:jakarta.validation-api:3.0.0'
  implementation 'org.hibernate.validator:hibernate-validator:7.0.5.Final'

  // JPA
  implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
  implementation 'org.eclipse.persistence:org.eclipse.persistence.jpa:4.0.1'
  compileOnly 'org.eclipse.persistence:eclipselink:4.0.2'
  implementation 'com.google.code.gson:gson:2.10.1'

  // Logging
  implementation "org.slf4j:slf4j-api:2.0.7"
  runtimeOnly "ch.qos.logback:logback-core:1.4.7"
  runtimeOnly "ch.qos.logback:logback-classic:1.4.7"


  // JWT
  implementation "io.jsonwebtoken:jjwt-api:0.11.5"
  runtimeOnly "io.jsonwebtoken:jjwt-impl:0.11.5"
  runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.11.5"

  // Tests
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
  useJUnitPlatform()
}


wildfly {
  controller = "remote+http://localhost:9990"
  username = "admin"
  password = "1234"
  deployment = "build/libs/${rootProject.name}-${version}.war"
  persistent = false
}

node {
  nodeProjectDir.set(file("${project.projectDir}/src/main/webapp/src"))
}

tasks.named("build") { finalizedBy("wildflyDeploy") }

war {
  exclude("src/**")
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn("yarn_build")
}